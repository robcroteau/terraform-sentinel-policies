# This policy uses the Sentinel tfplan import to require that
# all Google Cloud Storage Buckets, or Object Default Policies 
# prevent public access. This policy will allow individual public
# objects 

##### Imports #####

import "tfplan"
import "strings"

##### Functions #####

# Find all resources of a specific type from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resources = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {
    # Iterate over the named resources of desired type in the module
    for tfplan.module(path).resources[type] else {} as name, instances {
      # Iterate over resource instances
      for instances as index, r {

        # Get the address of the instance
        if length(path) == 0 {
          # root module
          address = type + "." + name + "[" + string(index) + "]"
        } else {
          # non-root module
          address = "module." + strings.join(path, ".module.") + "." +
                    type + "." + name + "[" + string(index) + "]"
        }

        # Add the instance to resources map, setting the key to the address
        resources[address] = r
      }
    }
  }

  return resources
}

# Validate that all instances of a specified resource type being modified have
# a specified top-level attribute in a given list
validated_predefined_and_default_not_in_list = func(type, attribute, disallowed_values) {

  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan(type)

  # Loop through the resource instances
  for resource_instances as address, r {

    # Skip resource instances that are being destroyed
    # to avoid unnecessary policy violations.
    # Used to be: if length(r.diff) == 0
    if r.destroy and not r.requires_new {
      print("Skipping resource", address, "that is being destroyed.")
      continue
    }
    
    if r.diff not contains attribute {
      # Skip attribute as its not defined
      continue
    }
    # Determine if the attribute is computed
    if r.diff[attribute].computed else false is true {
      print("Resource", address, "has attribute", attribute,
            "that is computed.")
      # Bucket or Object access should not be computed
      validated = false
    } else {
      # Validate that each instance does not have restricted values
      if r.applied[attribute] in disallowed_values {
        validated = false
        print("Predefined or Default ACL disallowed in: ", address)
      }
    }

  }
  return validated
}

# Validate that all instances of a specified resource type being modified have
# a specified top-level attribute in a given list
validate_acl_role_entity_not_in_list = func(type, attribute, disallowed_values) {

  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan(type)

  # Loop through the resource instances
  for resource_instances as address, r {

    # Skip resource instances that are being destroyed
    # to avoid unnecessary policy violations.
    if r.destroy and not r.requires_new {
      print("Skipping resource", address, "that is being destroyed.")
      continue
    }
    
    # Determine if the attribute is computed
    if r.diff[attribute].computed else false is true {
      print("Resource", address, "has attribute", attribute,
            "that is computed.")
      # Bucket or Object access should not be computed
      validated = false
    } else {
      if r.diff["role_entity.#"].computed else false is true {
        # Role_entity is not populated skip resource
        continue
      } 
      # Validate that each instance does not have restricted values
      results = filter r.applied[attribute] as role { role in disallowed_values }
      if length(results) > 0 {
        print("Invalid roles found: ", results)
        validated = false
      }
    }

  }
  return validated
}

validate_alluser_entity_roles = func(type) {
  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan(type)

  # Loop through the resource instances
  for resource_instances as address, r {
    # Skip resource instances that are being destroyed
    # to avoid unnecessary policy violations.
    # Used to be: if length(r.diff) == 0
    if r.destroy and not r.requires_new {
      print("Skipping resource", address, "that is being destroyed.")
      continue
    }
    print("Entity value: ", r.applied["entity"])
    # Determine if the attribute is computed
    if r.diff["entity"].computed {
      print("Resource", address, "has attribute: entity that is computed.")
      # Computed Entities are now allowed
      validated = false
    } else {
      # Validate that each instance does not have restricted values
      
      if r.applied["entity"] is "allUsers" or r.applied["entity"] is "allAuthenticatedUsers" {
        validated = false
        print("Resource: ", address, " contains disallowed reference to allUsers or allAuthenticatedUsers.")
      }
    }

  }
  return validated
}
##### Lists #####

# Disallowed GCP Entity Role Pairs
disallowed_types = [
  "READER:allUsers",
  "OWNER:allUsers",
]

disallowed_presets = [
  "publicRead",
  "publicReadWrite",
  "authenticatedRead",
]

##### Rules #####

# Call the validation function
# Three checks, 
# BUCKET/Object ACL for predefined/defaults
# Bucket/Object ACL for role_entities
# Bucket/Object for access control violations

object_level_default_validated = validated_predefined_and_default_not_in_list("google_storage_default_object_acl",
                     "default_acl", disallowed_presets)
bucket_level_default_validated = validated_predefined_and_default_not_in_list("google_storage_bucket_acl",
                     "default_acl", disallowed_presets)

object_level_predefined_validated = validated_predefined_and_default_not_in_list("google_storage_default_object_acl",
                     "predefined_acl", disallowed_presets)
bucket_level_predefined_validated = validated_predefined_and_default_not_in_list("google_storage_bucket_acl",
                     "predefined_acl", disallowed_presets)

object_level_acl_validated = validate_acl_role_entity_not_in_list("google_storage_default_object_acl",
                     "role_entity", disallowed_types)
bucket_level_acl_validated = validate_acl_role_entity_not_in_list("google_storage_bucket_acl",
                     "role_entity", disallowed_types)

object_level_access_control_validated = validate_alluser_entity_roles("google_storage_default_object_access_control")
bucket_level_access_control_validated = validate_alluser_entity_roles("google_storage_bucket_access_control")

bucket_level = rule {
  bucket_level_access_control_validated and bucket_level_acl_validated and bucket_level_predefined_validated and bucket_level_default_validated
}

object_level = rule {
  object_level_access_control_validated and object_level_acl_validated and object_level_predefined_validated and object_level_default_validated
}

# Main rule
main = rule {
  bucket_level and object_level
}


