##### Imports #####

import "tfplan"
import "strings"

##### Lists #####

# This policy only checks that the 'source_ranges' field in firewall rules does not use an
# open CIDR range of 0.0.0.0/0

##### Functions #####

# Find all resources of a specific type from all modules using the tfplan import
find_resources_from_plan = func(type) {

  resources = {}

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {
    # Iterate over the named resources of desired type in the module
    for tfplan.module(path).resources[type] else {} as name, instances {
      # Iterate over resource instances
      for instances as index, r {

        # Get the address of the instance
        if length(path) == 0 {
          # root module
          address = type + "." + name + "[" + string(index) + "]"
        } else {
          # non-root module
          address = "module." + strings.join(path, ".module.") + "." +
                    type + "." + name + "[" + string(index) + "]"
        }

        # Add the instance to resources map, setting the key to the address
        resources[address] = r
      }
    }
  }

  return resources
}

# Validate that a compute_firewall resource does not have an open source_range of 0.0.0.0/0
validate_no_open_sourcerange = func(type) {

  validated = true

  # Get all resource instances of the specified type
  resource_instances = find_resources_from_plan(type)

  # Loop through the resource instances
  for resource_instances as address, r {

    # Skip resource instances that are being destroyed
    # to avoid unnecessary policy violations.
    # Used to be: if length(r.diff) == 0
    if r.destroy and not r.requires_new {
      print("Skipping resource", address, "that is being destroyed.")
      continue
    }

    # Firewall json structure contains a "source_ranges" if specified
    if keys(r.applied) contains "source_ranges" {
      results = filter r.applied["source_ranges"] as range { strings.to_lower(range) contains "0.0.0.0/0" }
      if length(results) > 0 {
        print("Firewall source range is open 0.0.0.0/0: ", address)
        validated = false
      }
    } 
  }
  return validated
}



##### Rules #####

# Call the validation function
source_ranges_validated = validate_no_open_sourcerange("google_compute_firewall")
# Main rule
main = rule {
  source_ranges_validated
}